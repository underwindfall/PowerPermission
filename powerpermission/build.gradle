apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "de.mannodermaus.android-junit5"
apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"

android {
    compileSdkVersion androidCompileSdkVersion

    defaultConfig {
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode androidVersionCode
        versionName androidVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    testOptions {
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
            jacocoOptions {
                // here goes all jacoco config, for example
                html.enabled = true
                xml.enabled = false
                csv.enabled = false
            }
        }
        unitTests.all {
            testLogging.events = ["passed", "skipped", "failed"]
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation kotlinStdlib
    implementation kotlinReflect
    implementation androidXAppCompat
    compileOnly androidXAnnotations
    // spek
    testImplementation assertJCore
    testImplementation mockK
    testImplementation spekDslJvm
    testImplementation spekDslRunner
}

tasks.withType(Javadoc).all {
    enabled = false
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.source
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.source
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = groupProjectID
                artifactId = "powerpermission"
                version = publishVersionID
                artifact androidSourcesJar
                artifact androidJavadocsJar
                pom {
                    name = libName
                    description = descriptionLib
                    url = websiteLib
                    developers {
                        developer {
                            id = userId
                            name = userName
                        }
                    }
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    scm {
                        connection = gitUrl
                        developerConnection = gitUrl
                        url = websiteLib
                    }
                }
                from components.release
            }
        }
    }
}

Properties properties = new Properties()
def isLocal = project.rootProject.file("local.properties").exists()
if (isLocal) {
    properties.load(project.rootProject.file("local.properties").newDataInputStream())
}

bintray {
    user = !isLocal ? System.getenv("bintrayUser") : properties.getProperty("bintrayUser")
    key = !isLocal ? System.getenv("bintrayApiKey") : properties.getProperty("bintrayApiKey")
    pkg {
        repo = "maven"
        name = "powerpermission"
        websiteUrl = websiteLib
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        publish = true
        version {
            name = publishVersionID
        }
        publications = ['release']
    }
}
