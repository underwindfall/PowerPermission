apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"


android {
    compileSdkVersion androidCompileSdkVersion

    defaultConfig {
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode androidVersionCode
        versionName androidVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(path: ":powerpermission")
    implementation kotlinStdlib
    implementation androidXAppCompat
    api coroutines
    api coroutinesAndroid
    testImplementation "junit:junit:4.13"
    androidTestImplementation "androidx.test.ext:junit:1.1.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"
}

tasks.withType(Javadoc).all {
    enabled = false
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.source
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.source
}


afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = groupProjectID
                artifactId = "powerpermission-coroutines"
                version = publishVersionID
                artifact androidSourcesJar
                artifact androidJavadocsJar
                pom {
                    name = libName
                    description = descriptionLib
                    url = websiteLib
                    developers {
                        developer {
                            id = userId
                            name = userName
                        }
                    }
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    scm {
                        connection = gitUrl
                        developerConnection = gitUrl
                        url = websiteLib
                    }
                }
                from components.release
            }
        }
    }
}

bintray {
    user = System.getenv("bintrayUser")
    key = System.getenv("bintrayApiKey")

    configurations = ['archives']
    pkg {
        repo = "PowerPermission"
        name = "powerpermission-coroutines"
        websiteUrl = websiteLib
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        publish = true
        version {
            name = publishVersionID
        }
        publications = ['release']
    }
}